dnl Process this file with autoconf to produce a configure script.
AC_INIT
AC_CONFIG_SRCDIR(src/main.c)

BRASERO_MAJOR_VERSION=0
BRASERO_MINOR_VERSION=7
BRASERO_SUB=1
BRASERO_VERSION=$BRASERO_MAJOR_VERSION.$BRASERO_MINOR_VERSION.$BRASERO_SUB

AC_SUBST(BRASERO_MAJOR_VERSION)
AC_SUBST(BRASERO_MINOR_VERSION)
AC_SUBST(BRASERO_SUB)
AC_SUBST(BRASERO_VERSION)

LT_REVISION=1
LT_AGE=1
LT_CURRENT=1

AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)


AC_DEFINE_UNQUOTED(BRASERO_MAJOR_VERSION, $BRASERO_MAJOR_VERSION, [major version])
AC_DEFINE_UNQUOTED(BRASERO_MINOR_VERSION, $BRASERO_MINOR_VERSION, [minor version])
AC_DEFINE_UNQUOTED(BRASERO_SUB, $BRASERO_SUB, [sub version])
AC_DEFINE_UNQUOTED(BRASERO_VERSION, $BRASERO_VERSION, [version])

AM_INIT_AUTOMAKE(brasero, $BRASERO_VERSION)
AM_CONFIG_HEADER(config.h)

dnl ***************** GENERAL **********************************

AC_DISABLE_STATIC
AC_PROG_LIBTOOL
dnl ** reminder: the following set CFLAGS to -O2 -g if empty
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

dnl ***************** SCSI related *****************************
AC_SUBST(BRASERO_SCSI_LIBS)
AC_CHECK_HEADERS([camlib.h],[has_cam="yes"],[has_cam="no"])

if test x"$has_cam" = x"yes"; then
    BRASERO_SCSI_LIBS="-lcam"
else

dnl ***************** check for linux sg interface *************
AC_CHECK_TYPES([sg_io_hdr_t],[has_sg="yes"],[has_sg="no"],
[#include <sys/types.h>
 #include <scsi/sg.h>])

if test x"$has_sg" = x"no"; then
	AC_ERROR([Linux sg interface headers could not be found])
fi
fi
AM_CONDITIONAL(HAVE_CAM_LIB_H, test x"$has_cam" = "xyes")
AM_CONDITIONAL(HAVE_SG_IO_HDR_T, test x"$has_sg" = "xyes")

dnl ***************** LARGE FILE SUPPORT ***********************

AC_SYS_LARGEFILE

dnl ********** GNOME2 ******************************
GLIB_REQUIRED=2.15.6
GMODULE_REQUIRED=2.6.0
GIO_REQUIRED=2.15.5
GDK_REQUIRED=2.6.0
GTK_REQUIRED=2.11.6
LIBGNOME_REQUIRED=2.10.0
LIBGNOMEUI_REQUIRED=2.10.0
GCONF_REQUIRED=2.0.0
GSTREAMER_REQUIRED=0.10.6
GSTREAMER_BASE_REQUIRED=0.10.0
LIBXML2_REQUIRED=2.6.0
HAL_REQUIRED=0.5

dnl ** used by brasero and one plugin
PKG_CHECK_MODULES(BRASERO_GSTREAMER, 		\
	gstreamer-0.10 >= $GSTREAMER_REQUIRED \
	gstreamer-plugins-base-0.10 >= $GSTREAMER_BASE_REQUIRED)
echo $CFLAGS
AC_SUBST(BRASERO_GSTREAMER_CFLAGS)
AC_SUBST(BRASERO_GSTREAMER_LIBS)

BRASERO_GSTREAMER_CFLAGS="$BRASERO_GSTREAMER_CFLAGS $CFLAGS"
BRASERO_GSTREAMER_LIBS="$BRASERO_GSTREAMER_LIBS $LDFLAGS"

dnl ** used by brasero and one plugin
PKG_CHECK_MODULES(BRASERO_GIO, 	\
	gio-2.0 >= $GIO_REQUIRED)

AC_SUBST(BRASERO_GIO_CFLAGS)
AC_SUBST(BRASERO_GIO_LIBS)

BRASERO_GIO_CFLAGS="$BRASERO_GIO_CFLAGS $CFLAGS"
BRASERO_GIO_LIBS="$BRASERO_GIO_LIBS $LDFLAGS"

dnl ** used by brasero and all modules
PKG_CHECK_MODULES(BRASERO_BASE, 		\
	gconf-2.0 >= $GCONF_REQUIRED		\
	glib-2.0 >= $GLIB_REQUIRED)

AC_SUBST(BRASERO_BASE_CFLAGS)
AC_SUBST(BRASERO_BASE_LIBS)

BRASERO_BASE_CFLAGS="$BRASERO_BASE_CFLAGS $BRASERO_GIO_CFLAGS $CFLAGS"
BRASERO_BASE_LIBS="$BRASERO_BASE_LIBS $LDFLAGS"

dnl ** used only by brasero app itself ***
PKG_CHECK_MODULES(BRASERO,			\
	hal >= $HAL_REQUIRED		\
	gmodule-2.0 >= $GMODULE_REQUIRED 	\
	gdk-2.0 >= $GDK_REQUIRED	\
	gtk+-2.0 >= $GTK_REQUIRED		\
	libgnome-2.0 >= $LIBGNOME_REQUIRED	\
	libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED	\
	libxml-2.0 >= $LIBXML2_REQUIRED)

AC_SUBST(BRASERO_CFLAGS)
AC_SUBST(BRASERO_LIBS)
BRASERO_CFLAGS="$BRASERO_BASE_CFLAGS $BRASERO_GSTREAMER_CFLAGS $BRASERO_GIO_CFLAGS $BRASERO_CFLAGS $CFLAGS"
BRASERO_LIBS="$BRASERO_BASE_LIBS $BRASERO_GSTREAMER_LIBS $BRASERO_GIO_LIBS $BRASERO_LIBS $BRASERO_SCSI_LIBS $LDFLAGS"

dnl ****************check for libburn (optional)**************
LIBBURN_REQUIRED=0.4.0
LIBISOFS_REQUIRED=0.2.8

AC_ARG_ENABLE(libburnia,
			[  --enable-libburnia		Build libburnia plugins [[default = yes]]],,
			[enable_libburnia="yes"])

if test x"$enable_libburnia" = "xyes"; then
	PKG_CHECK_MODULES(LIBBURNIA, libburn-5 >= $LIBBURN_REQUIRED  libisofs-1 >= $LIBISOFS_REQUIRED, build_libburnia=yes, build_libburnia=no)
else
	build_libburnia="no"
fi

if test x"$build_libburnia" = "xyes"; then
	AC_SUBST(BRASERO_LIBBURNIA_CFLAGS)
	AC_SUBST(BRASERO_LIBBURNIA_LIBS)

	BRASERO_LIBBURNIA_CFLAGS="$LIBBURNIA_CFLAGS"
	BRASERO_LIBBURNIA_LIBS="$LIBBURNIA_LIBS"
	AC_DEFINE(BUILD_LIBBURNIA, 1, [define if you  want to build libburnia plugins])
fi
AM_CONDITIONAL(BUILD_LIBBURNIA, test x"$build_libburnia" = "xyes")

dnl **************** check for cdrtools **********************
AC_ARG_ENABLE(cdrtools,
			[  --enable-cdrtools		Build cdrtools plugins [[default = yes]]],,
			[build_cdrtools="yes"])

if test x"$build_cdrtools" = "xyes"; then
	AC_DEFINE(BUILD_CDRTOOLS, 1, [define if you  want to build cdrtools plugins])
else
	build_cdrtools="no"
fi
AM_CONDITIONAL(BUILD_CDRTOOLS, test x"$build_cdrtools" = "xyes")

dnl **************** check for cdrkit ************************
AC_ARG_ENABLE(cdrkit,
			[  --enable-cdrkit		Build cdrkit plugins [[default = yes]]],,
			[build_cdrkit="yes"])

if test x"$build_cdrkit" = "xyes"; then
	AC_DEFINE(BUILD_CDRKIT, 1, [define if you  want to build cdrkit plugins])
else
	build_cdrkit="no"
fi
AM_CONDITIONAL(BUILD_CDRKIT, test x"$build_cdrkit" = "xyes")

dnl ****************check for libnotify (optional)**************
LIBNOTIFY_REQUIRED=0.3.0

AC_ARG_ENABLE(libnotify,
			[  --enable-libnotify		use libnotify if available [[default = yes]]],,
			[enable_libnotify="yes"])

if test x"$enable_libnotify" = "xyes"; then
	PKG_CHECK_MODULES(LIBNOTIFY, libnotify >= $LIBNOTIFY_REQUIRED, build_libnotify=yes, build_libnotify=no)
else
	build_libnotify="no"
fi

if test x"$build_libnotify" = "xyes"; then
	BRASERO_CFLAGS="$BRASERO_CFLAGS $LIBNOTIFY_CFLAGS"
	BRASERO_LIBS="$BRASERO_LIBS $LIBNOTIFY_LIBS"
	AC_DEFINE(HAVE_LIBNOTIFY, 1, [define if you  want to use with libnotify support])
fi
AM_CONDITIONAL(HAVE_LIBNOTIFY, test x"$build_libnotify" = "xyes")

dnl ****************check for search (optional)**************
BEAGLE_REQUIRED=0.3.0

AC_ARG_ENABLE(search,
			[  --enable-search		build search pane (if beagle is available) [[default = yes]]],,
			[enable_search="yes"])

if test x"$enable_search" = "xyes"; then
	PKG_CHECK_MODULES(BEAGLE, libbeagle-1.0 >= $BEAGLE_REQUIRED, build_beagle=yes, build_beagle=no)
else
	build_beagle="no"
fi

if test x"$build_beagle" = "xyes"; then
	BRASERO_CFLAGS="$BRASERO_CFLAGS $BEAGLE_CFLAGS"
	BRASERO_LIBS="$BRASERO_LIBS $BEAGLE_LIBS"
	AC_DEFINE(BUILD_SEARCH, 1, [define if you  want to use search pane])
fi
AM_CONDITIONAL(BUILD_SEARCH, test x"$build_search" = "xyes")

dnl ****************check for playlist (optional)**************
TOTEM_REQUIRED=2.20.0

AC_ARG_ENABLE(playlist,
			[  --enable-playlist		build playlist pane (if totem is available)[[default = yes]]],,
			[enable_playlist="yes"])

if test x"$enable_playlist" = "xyes"; then
	PKG_CHECK_MODULES(TOTEM, totem-plparser >= $TOTEM_REQUIRED, build_totem=yes, build_totem=no)
else
	build_totem="no"
fi

if test x"$build_totem" = "xyes"; then
	BRASERO_CFLAGS="$BRASERO_CFLAGS $TOTEM_CFLAGS"
	BRASERO_LIBS="$BRASERO_LIBS $TOTEM_LIBS"
	AC_DEFINE(BUILD_PLAYLIST, 1, [define if you  want to build playlist pane])
fi
AM_CONDITIONAL(BUILD_PLAYLIST, test x"$build_totem" = "xyes")

dnl ****************check for dbus (optional)**************
DBUS_REQUIRED=0.7.2

AC_ARG_ENABLE(dbus,
			[  --enable-dbus		build dbus (if Gnome Power Manager is available)[[default = yes]]],,
			[enable_dbus="yes"])

if test x"$enable_dbus" = "xyes"; then
	PKG_CHECK_MODULES(DBUS, dbus-glib-1 >= $DBUS_REQUIRED, build_dbus=yes, build_dbus=no)
else
	build_dbus="no"
fi

if test x"$build_dbus" = "xyes"; then
	BRASERO_CFLAGS="$BRASERO_CFLAGS $DBUS_CFLAGS"
	BRASERO_LIBS="$BRASERO_LIBS $DBUS_LIBS"
	AC_DEFINE(BUILD_DBUS, 1, [define if you want to build dbus])
fi
AM_CONDITIONAL(BUILD_PLAYLIST, test x"$build_dbus" = "xyes")


dnl ****************check for preview (optional)**************
GSTREAMER_MODULE_REQUIRED=0.10.0
AC_ARG_ENABLE(preview,
			[  --enable-preview		build preview pane [[default = yes]]],,
			[enable_preview="yes"])

if test x"$enable_preview" = "xyes"; then
	PKG_CHECK_MODULES(GSTREAMER_GCONF, gstreamer-plugins-base-0.10 >= $GSTREAMER_MODULE_REQUIRED, build_preview=yes, build_preview=no)
else
	build_preview="no"
fi

if test x"$build_preview" = "xyes"; then
	BRASERO_CFLAGS="$BRASERO_CFLAGS $GSTREAMER_GCONF_CFLAGS"
	BRASERO_LIBS="$BRASERO_LIBS $GSTREAMER_GCONF_LIBS -lgstinterfaces-0.10"
	AC_DEFINE(BUILD_PREVIEW, 1, [define if you  want to build preview pane])
fi
AM_CONDITIONAL(BUILD_PREVIEW, test x"$build_preview" = "xyes")

dnl ****************check for inotify (optional)**************
AC_ARG_ENABLE(inotify,
			[  --enable-inotify		use inotify [[default = yes]]],,
			[enable_inotify="yes"])

if test x"$enable_inotify" = "xyes"; then
	AC_DEFINE(BUILD_INOTIFY, 1, [define if you  want to build inotify])
fi
AM_CONDITIONAL(BUILD_INOTIFY, test x"$enable_inotify" = "xyes")

dnl ****** Update mime, desktop and icon caches *******
AC_ARG_ENABLE(caches,
			[  --enable-caches	Run update-* to update mime, desktop and icon caches when installing [[default = yes]]],,
			[enable_caches="yes"])

AM_CONDITIONAL(UPDATE_CACHES, test x"$enable_caches" = "xyes")

dnl ****** INTERNATIONALIZATION **********************
GETTEXT_PACKAGE=brasero
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Package name for gettext])

AM_GLIB_GNU_GETTEXT

IT_PROG_INTLTOOL([0.35.0])

if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale", [Location of package locale files])
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale", [Location of package locale files])
fi

dnl ***** GCONF *************************

AC_ARG_ENABLE(schemas-install,AC_HELP_STRING([--disable-schemas-install],
					     [Disable installation of the gconf schemas]))

AM_CONDITIONAL(SCHEMAS_INSTALL, test x$enable_schemas_install != xno)

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
if test x"$GCONFTOOL" = "xno"; then
	AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi
AM_GCONF_SOURCE_2

dnl ****** END ***********************

AC_OUTPUT([
Makefile
docs/Makefile
data/Makefile
data/brasero.desktop.in
data/icons/Makefile
src/plugins/Makefile
src/plugins/cdrdao/Makefile
src/plugins/cdrkit/Makefile
src/plugins/cdrtools/Makefile
src/plugins/growisofs/Makefile
src/plugins/libburnia/Makefile
src/plugins/transcode/Makefile
src/plugins/dvdcss/Makefile
src/plugins/checksum/Makefile
src/plugins/local-track/Makefile
po/Makefile.in
src/Makefile
])

echo
echo
echo "$PACKAGE configuration summary:"
echo "----------------------------------"

echo "Version: $BRASERO_VERSION
	Update caches: ${enable_caches}
	Build inotify: ${enable_inotify}
	Build search pane : ${build_beagle}
	Build playlist pane : ${build_totem}
	Build Preview pane : ${enable_preview}
	Build libnotify support : ${build_libnotify}
	Build dbus support : ${build_dbus}
	Build cdrtools plugins : ${build_cdrtools}
	Build cdrkit plugins : ${build_cdrkit}
	Build libburnia plugins : ${build_libburnia}
"
echo
echo
